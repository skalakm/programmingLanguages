
// purpose: defining a CUP parser for a simple calculator

import java_cup.runtime.*;

parser code {:
    public static void main(String args[]) throws Exception {
        new parser(new Yylex(System.in)).parse();
    }
:}

terminal LPAR, RPAR, SEMI;
terminal String ADDOP, MULOP;
terminal Double NUM;

nonterminal exprList;
nonterminal Double expr;

precedence left ADDOP;
precedence left MULOP;


exprList ::= exprList expr:e SEMI {: System.out.println(e); :}
        |  {: System.out.println(); :}
        ;

expr  ::= NUM:n {: RESULT = n; :}
        | expr:l ADDOP:op expr:r
          {: if (op.equals("+")) {
               RESULT = new Double(l.doubleValue() + r.doubleValue()); 
             } else {
               RESULT = new Double(l.doubleValue() - r.doubleValue()); 
             }
          :}
        | expr:l MULOP:op expr:r
          {: if (op.equals("*")) { 
               RESULT = new Double(l.doubleValue() * r.doubleValue()); 
             } else {
               RESULT = new Double(l.doubleValue() / r.doubleValue()); 
             }
          :}
        | LPAR expr:e RPAR {: RESULT = e; :}
        ;
     
